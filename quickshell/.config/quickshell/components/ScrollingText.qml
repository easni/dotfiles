import QtQuick

import "../styles"

Item {
    id: root

    property string text: ""
    property int maxWidth: 100
    property int fontSize: 13

    width: maxWidth
    implicitHeight: label.implicitHeight

    clip: true

    Text {
        id: label

        text: root.text

        color: Theme.textPrimary

        font.family: "JetBrainsMono Nerd Font"
        font.pixelSize: root.fontSize
        font.bold: true

        anchors.verticalCenter: parent.verticalCenter

        x: 0
    }

    SequentialAnimation {
        id: scrollAnimation

        loops: Animation.Infinite

        PauseAnimation {
            duration: 2000
        }

        NumberAnimation {
            target: label
            property: "x"

            // to: -(label.width - root.maxWidth)
            to: -(label.width)

            // duration: 8000
            duration: (label.width / 50) * 1000

            easing.type: Easing.Linear
        }

        NumberAnimation {
            target: label
            property: "x"

            to: root.maxWidth

            // duration: 8000
            duration: 0

            easing.type: Easing.Linear
        }

        NumberAnimation {
            target: label
            property: "x"

            to: 0

            // duration: 8000
            duration: (root.maxWidth / 50) * 1000

            easing.type: Easing.Linear
        }
    }

    function shouldScroll() {
        return label.width > root.maxWidth + 20
    }

    onTextChanged: {
        scrollAnimation.stop()

        label.x = 0

        if (shouldScroll()) {
            scrollAnimation.start()
        }
    }

    // Restart scrolling only after the Text item has been laid out.
    // The width is not updated immediately when the text changes.
    Connections {
        target: label

        function onWidthChanged() {
            scrollAnimation.stop()
            label.x = 0

            if (shouldScroll())
                scrollAnimation.start()
        }
    }

    Component.onCompleted: {
        if (shouldScroll()) {
            scrollAnimation.start()
        }
    }
}
